//@version=5
strategy("Supertrend Strategy", overlay=true, default_qty_type=strategy.cash, default_qty_value=500,initial_capital = 500)
Periods1 = input.int(title="ATR Period 1", defval=14)
Multiplier1 = input.float(title="ATR Multiplier 1",  step=0.1, defval=2.0)
Periods2 = input.int(title="ATR Period 2", defval=21)
Multiplier2 = input.float(title="ATR Multiplier 2",  step=0.1, defval=1.0)
takeprofit_percent = input.float(title="takeprofit_percent",step = 0.5,defval = 10)
IsTakeprofit = input.bool(title = "Is TakeProfit",defval = true)


heikinashi_close = request.security(ticker.heikinashi(syminfo.tickerid), timeframe.period, close)
heikinashi_open = request.security(ticker.heikinashi(syminfo.tickerid), timeframe.period, open)
heikinashi_high = request.security(ticker.heikinashi(syminfo.tickerid), timeframe.period, high)
heikinashi_low = request.security(ticker.heikinashi(syminfo.tickerid), timeframe.period, low)


atr(length) =>
    trueRange = na(heikinashi_high[1])? heikinashi_high-heikinashi_low : math.max(math.max(heikinashi_high - heikinashi_low, math.abs(heikinashi_high - heikinashi_close[1])), math.abs(heikinashi_low - heikinashi_close[1]))
    //true range can be also calculated with ta.tr(true)
    ta.rma(trueRange, length)

pine_supertrend(factor, atrPeriod) =>
    src = heikinashi_close
    atr = atr(atrPeriod)
    upperBand = src + factor * atr
    lowerBand = src - factor * atr
    prevLowerBand = nz(lowerBand[1])
    prevUpperBand = nz(upperBand[1])

    lowerBand := lowerBand > prevLowerBand or heikinashi_close[1] < prevLowerBand ? lowerBand : prevLowerBand
    upperBand := upperBand < prevUpperBand or heikinashi_close[1] > prevUpperBand ? upperBand : prevUpperBand
    int direction = na
    float superTrend = na
    prevSuperTrend = superTrend[1]
    if na(atr[1])
        direction := 1
    else if prevSuperTrend == prevUpperBand
        direction := heikinashi_close > upperBand ? -1 : 1
    else
        direction := heikinashi_close < lowerBand ? 1 : -1
    superTrend := direction == -1 ? lowerBand : upperBand
    [superTrend, direction]
 
tr() =>
    math.max(heikinashi_high - heikinashi_low, math.abs(heikinashi_high - heikinashi_close[1]), math.abs(heikinashi_low - heikinashi_close[1]))
 

[pineSupertrend1, pineDirection1] = pine_supertrend(Multiplier1, Periods1)
[pineSupertrend2, pineDirection2] = pine_supertrend(Multiplier2, Periods2)


plot(pineDirection1 < 0 ? pineSupertrend1 : na, "Up supertrend1", style=plot.style_linebr, linewidth=1, color=color.green)
plot(pineDirection1 > 0 ? pineSupertrend1 : na, "Down supertrend1", style=plot.style_linebr, linewidth=1, color=color.red)
plot(pineDirection2 < 0 ? pineSupertrend2 : na, "Up supertrend2", style=plot.style_linebr, linewidth=1, color=color.green)
plot(pineDirection2 > 0 ? pineSupertrend2 : na, "Down supertrend2", style=plot.style_linebr, linewidth=1, color=color.red)


plotcandle(heikinashi_open,heikinashi_high,heikinashi_low,heikinashi_close, color = open < close ? color.green : color.red,editable = true)


var dir = 0 // 0 = long 1 = short 
if pineDirection1 < 0 and pineDirection2 < 0
    dir := 0
if pineDirection1 > 0 and pineDirection2 > 0
    dir := 1



plot(dir == 0 ? pineSupertrend1<pineSupertrend2?pineSupertrend1:pineSupertrend2 : na, "Up direction", style=plot.style_linebr, linewidth=2, color=color.green)
plot(dir == 1 ? pineSupertrend1>pineSupertrend2?pineSupertrend1:pineSupertrend2 : na, "Down direction", style=plot.style_linebr, linewidth=2, color=color.red)



if ta.change(dir)<0
    strategy.close_all(alert_message = "true")
    strategy.entry("Long", strategy.long,alert_message = "false")

if ta.change(dir)>0
    strategy.close_all(alert_message = "true")
    strategy.entry("Short", strategy.short,alert_message = "false")

if (ta.change(pineDirection1) or ta.change(pineDirection2)) and (not ta.change(dir)) and IsTakeprofit
    if dir == 0 and (pineDirection1 != pineDirection2)
        strategy.close("Long",qty_percent = takeprofit_percent)
    if dir == 1 and (pineDirection1 != pineDirection2)
        strategy.close("Short",qty_percent = takeprofit_percent)